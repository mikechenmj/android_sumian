apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    signingConfigs {

        configs rootProject.ext.configs

    }

    defaultConfig {

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionName rootProject.ext.android.libVersionName
        versionCode rootProject.ext.android.libVersionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            //设置支持的SO库架构  //默认 x5 只支持32位 所以只设置这一个架构，很无奈
            abiFilters 'armeabi-v7a'
        }

        resConfigs "zh"//,"en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {

        rootProject.ext.lintOptions

    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    //api fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    //noinspection GradleCompatible
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    api files('libs/tbs_sdk_thirdapp_v3.6.0.1352_43618_sharewithdownload_withoutGame_obfs_20180918_120327.jar')
}

//项目主页
def siteUrl = 'http://192.168.0.153/rysn/sd/sd_common_android/blob/master/README.md'
//项目的git地址
def gitUrl = 'http://192.168.0.153/rysn/sd/sd_common_android.git'
//发布到JCenter上的项目名字
def libName = "sm-jsbridge"

//发布到组织名称名字，必须填写
//compile 'com.sumian.sd:common:1.0.0'
group = "com.sumian.sd"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0.2"

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成Javadoc文档
task javadoc(type: Javadoc) {
    options.encoding = "utf-8"//设置 javadoc 文档的编码格式
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc).all {
    enabled = false  //关键部分 关闭 doc 文件的检测
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
}

//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到JCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                //项目描述，随意填
                name 'a sumian sm-jsbridge component'
                url siteUrl
                licenses {
                    license {
                        //开源协议
                        name 'GNU General Public License, Version 3.0'
                        url 'http://www.gnu.org/licenses/quick-guide-gplv3.html'
                    }
                }
                developers {
                    developer {
                        //开发者的个人信息
                        id 'sumiantech'
                        name 'sumian'
                        email 'develop@sumian.tech'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        //注意：这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = "sm-jsbridge"
        //发布到JCenter上的项目名字
        name = libName
        //项目描述
        desc = 'a sumian sm-jsbridge component'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["GPL-3.0"]
        publish = true
    }
}

javadoc {
    options {
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}

