apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.android.library'


//项目主页
def siteUrl = 'http://120.79.47.8/rysn/sd/sd_common_android/blob/master/README.md'
//项目的git地址
def gitUrl = 'http://120.79.47.8/rysn/sd/sd_common_android.git'
//发布到JCenter上的项目名字
def libName = "common"

//发布到组织名称名字，必须填写
//compile 'com.sumian.sd:common:1.0.0'
group = "com.sumian.sd"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0.0"

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成Javadoc文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到JCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                //项目描述，随意填
                name 'a sumian common component'
                url siteUrl
                licenses {
                    license {
                        //开源协议
                        name 'GNU General Public License, Version 3.0'
                        url 'http://www.gnu.org/licenses/quick-guide-gplv3.html'
                    }
                }
                developers {
                    developer {
                        //开发者的个人信息
                        id 'sumiantech'
                        name 'sumian'
                        email 'develop@sumian.tech'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        //注意：这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = "common"
        //发布到JCenter上的项目名字
        name = libName
        //项目描述
        desc = 'a sumian common component'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["GPLv3"]
        publish = true
    }
}

javadoc {
    options {
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}